plugins {
    id 'maven-publish'
    // id 'com.github.johnrengelman.shadow' version '5.2.0' // do not update unless https://github.com/johnrengelman/shadow/issues/651 is fixed
}

dependencies {
    implementation project(':natparse')
    implementation libraries.reflections
    implementation libraries.slf4j_nop
    implementation libraries.picocli

    testImplementation project(':testhelpers')
}

// shadowJar {
//     archiveBaseName.set('natlint')
//     archiveClassifier.set('')
//     archiveVersion.set('')
// }

jar {
    manifest {
        attributes(
            'Main-Class': 'org.amshove.natlint.App',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenNatlint(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = 'Static code analysis for Software AGs Natural language'
                url = 'https://github.com/MarkusAmshove/natls'
                scm {
                    connection = 'scm:git:https://github.com/MarkusAmshove/natls.git'
                    developerConnection = 'scm:git:git@github.com:MarkusAmshove/natls.git'
                    url = 'https://github.com/MarkusAmshove/natls'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/MarkusAmshove/natls/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'markusamshove'
                        name = 'Markus Amshove'
                    }
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenNatlint
}

