plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'org.beryx.jlink' version '2.25.0'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
}

dependencies {
    implementation project(':natparse')
    implementation libraries.reflections
    implementation libraries.slf4j_nop
    implementation libraries.picocli

    testImplementation project(':testhelpers')
}

shadowJar {
    archiveBaseName.set('natlint')
    archiveClassifier.set('')
    archiveVersion.set('')
}

application {
    mainClassName = 'org.amshove.natlint.App'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.amshove.natlint.App',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

compileTestJava {
    moduleOptions {
        compileOnClasspath = true
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageName = 'natlint'
    launcher {
        name = 'natlint'
    }
    targetPlatform('linux') {
        jdkHome = jdkDownload('https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.4.1%2B1/OpenJDK17U-jdk_x64_linux_hotspot_17.0.4.1_1.tar.gz')
    }
    targetPlatform('win') {
        jdkHome = jdkDownload('https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.4.1%2B1/OpenJDK17U-jdk_x64_windows_hotspot_17.0.4.1_1.zip')
    }
}
