plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'org.beryx.jlink' version '2.25.0'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
}

dependencies {
    implementation libraries.slf4j_nop
    implementation libraries.lsp4j
    implementation libraries.reflections
    implementation project(':natparse')
    implementation project(':natlint')

    testImplementation project(':testhelpers')
}

shadowJar {
    archiveBaseName.set('natls')
    archiveClassifier.set('')
    archiveVersion.set('')
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.amshove.natls.App',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageName = 'natls'
    launcher {
        name = 'natls'
        mainClassName = 'org.amshove.natls.App'
    }
    mergedModule {
        additive = true
        requires 'com.google.gson'
    }
    targetPlatform('linux') {
        jdkHome = jdkDownload('https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.4.1%2B1/OpenJDK17U-jdk_x64_linux_hotspot_17.0.4.1_1.tar.gz')
    }
    targetPlatform('win') {
        jdkHome = jdkDownload('https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.4.1%2B1/OpenJDK17U-jdk_x64_windows_hotspot_17.0.4.1_1.zip')
    }
}
