plugins {
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

dependencies {
    implementation libraries.lsp4j
    implementation libraries.reflections
	implementation libraries.slf4j_utillogging
    implementation project(':natparse')
    implementation project(':natlint')

    testImplementation project(':testhelpers')
    testImplementation libraries.slf4j_nop
}

shadowJar {
    archiveBaseName.set('natls')
    archiveClassifier.set('')
    archiveVersion.set('')
}

configurations.testImplementation {
    exclude group: 'org.slf4j', module: libraries.slf4j_utillogging.split(':')[1]
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.amshove.natls.App',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/markusamshove/natls"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}