dependencies {
    implementation libraries.lsp4j
    implementation libraries.reflections
	implementation libraries.slf4j_utillogging
    implementation project(':natparse')
    implementation project(':natlint')

    testImplementation project(':testhelpers')
    testImplementation libraries.slf4j_nop
    testImplementation 'org.awaitility:awaitility:4.2.0'
}

tasks.register('fatJar', Jar) {
    manifest {
        attributes(
                'Main-Class': 'org.amshove.natls.App',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    archiveFileName = 'natls.jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(':natparse:jar', ':natlint:jar')
    from {configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

configurations.testImplementation {
    exclude group: 'org.slf4j', module: libraries.slf4j_utillogging.split(':')[1]
}

jar {
    manifest {
        attributes(
            'Main-Class': 'org.amshove.natls.App',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

artifacts {
    jar
}