plugins {
	id 'java-library'
	id 'org.sonarqube' version '2.7'
	id "com.diffplug.spotless" version "6.2.0"
}

ext {
	JAVA_VERSION = 17
	SLF4J_VERSION = '1.7.32'
    SONAR_VERSION = '8.5.1.38104'
	libraries = [
		// Implementation
		guava : 'com.google.guava:guava:31.0.1-jre',
		lsp4j : 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.15.0',
		slf4j_api	: "org.slf4j:slf4j-api:${SLF4J_VERSION}",
		slf4j_simple : "org.slf4j:slf4j-simple:${SLF4J_VERSION}",
		slf4j_nop	: "org.slf4j:slf4j-nop:${SLF4J_VERSION}",
		reflections  : 'org.reflections:reflections:0.10.2',
		picocli : 'info.picocli:picocli:4.6.3',
        sonarqube_api   : "org.sonarsource.sonarqube:sonar-plugin-api:${SONAR_VERSION}",
		// Test
		junit_jupiter : 'org.junit:junit-bom:5.8.2',
		assertJ : 'org.assertj:assertj-core:3.23.1',
		mockito : 'org.mockito:mockito-all:1.10.19',
		archUnit: 'com.tngtech.archunit:archunit:0.22.0'
	]
}

spotless {
	format 'gradle', {
		target '.gradle'
		indentWithTabs()
	}
	java {
		target '**/src/*/java/**/*.java'
		toggleOffOn('"""', '"""')
		indentWithTabs()
	}
}

allprojects {
	apply plugin: 'java-library'
	apply plugin: 'jacoco'

	group = 'org.amshove'
	version = '0.1'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
		}
	}

	tasks.withType(JavaCompile).configureEach {
		options.release.set(JAVA_VERSION)
	}

	repositories {
		mavenCentral()
	}
}


subprojects {
	dependencies {
		implementation libraries.guava

		testImplementation libraries.assertJ
		testImplementation(platform(libraries.junit_jupiter))
		testImplementation('org.junit.jupiter:junit-jupiter')
	}

	test {
		useJUnitPlatform()
		testLogging {
			events "skipped", "failed"
			testLogging.showStandardStreams = true
			testLogging.exceptionFormat = 'full'
		}
	}

	jacocoTestReport {
			additionalSourceDirs.setFrom files(sourceSets.main.allSource.srcDirs)
			sourceDirectories.setFrom files(sourceSets.main.allSource.srcDirs)
			classDirectories.setFrom  files(sourceSets.main.output)
			reports {
				html.required = true
				xml.required = true
				csv.required = false
			}
	}
}

task cover(type: JacocoReport) {
	dependsOn = subprojects.test
	dependsOn += test

	additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
	sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories.setFrom files(subprojects.sourceSets.main.output)
	executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')

	reports {
		html.required = true
		xml.required = true
		csv.required = false
	}
}

task testReport(type: TestReport) {
	destinationDir = file("$buildDir/reports/allTests")
	reportOn subprojects.collect { it.tasks.withType(Test) }
}

sonarqube {
	properties {
		property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
	}
}

project.tasks['sonarqube'].dependsOn subprojects.jacocoTestReport
