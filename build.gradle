plugins {
	id 'java-library'
	id 'org.sonarqube' version '3.4.0.2513'
	id "com.diffplug.spotless" version "6.2.0"
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin'  version '1.3.0'
}

ext {
	JAVA_VERSION = 17
	SLF4J_VERSION = '1.7.32'
    SONAR_VERSION = '9.9.1.69595'
    SONAR_API_VERSION = '10.1.0.809'
	libraries = [
		// Implementation
		guava : 'com.google.guava:guava:31.0.1-jre',
		lsp4j : 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.21.1',
		dom4j : 'org.dom4j:dom4j:2.1.3',
		slf4j_api	: "org.slf4j:slf4j-api:${SLF4J_VERSION}",
		slf4j_simple : "org.slf4j:slf4j-simple:${SLF4J_VERSION}",
		slf4j_nop	: "org.slf4j:slf4j-nop:${SLF4J_VERSION}",
		slf4j_utillogging : "org.slf4j:slf4j-jdk14:${SLF4J_VERSION}",
		reflections  : 'org.reflections:reflections:0.10.2',
		picocli : 'info.picocli:picocli:4.6.3',
        sonarqube_api   : "org.sonarsource.api.plugin:sonar-plugin-api:${SONAR_API_VERSION}",
        sonarqube_test_fixtures : "org.sonarsource.api.plugin:sonar-plugin-api-test-fixtures:${SONAR_API_VERSION}",
        sonarqube_test_api : "org.sonarsource.sonarqube:sonar-plugin-api-impl:${SONAR_VERSION}",
		// Test
		junit_jupiter : 'org.junit:junit-bom:5.8.2',
		assertJ : 'org.assertj:assertj-core:3.23.1',
		mockito : 'org.mockito:mockito-all:1.10.19',
		archUnit: 'com.tngtech.archunit:archunit:0.22.0'
	]
}

spotless {
	format 'gradle', {
		target '.gradle'
		indentWithTabs()
	}
	java {
		target '**/src/*/java/**/*.java'
		toggleOffOn('"""', '"""')
		indentWithTabs()
		eclipse().configFile('EclipseFormatter.xml')
	}
}

allprojects {
	apply plugin: 'java-library'
	apply plugin: 'jacoco'

	group = 'org.amshove.natural'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
		}
	}

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

	tasks.withType(JavaCompile).configureEach {
		options.release.set(JAVA_VERSION)
	}

	repositories {
		mavenCentral()
	}

	javadoc.options.addStringOption('Xdoclint:none', '-quiet')
}


subprojects {
	dependencies {
		implementation libraries.guava

		testImplementation libraries.assertJ
		testImplementation(platform(libraries.junit_jupiter))
		testImplementation('org.junit.jupiter:junit-jupiter')
	}

	test {
		useJUnitPlatform()
		testLogging {
			events "skipped", "failed"
			testLogging.showStandardStreams = true
			testLogging.exceptionFormat = 'full'
		}
	}

	jacocoTestReport {
		reports {
			html.required = true
			xml.required = true
			csv.required = false
		}
	}
}

task cover(type: JacocoReport) {
	dependsOn = subprojects.test
	dependsOn += test

	additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
	sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories.setFrom files(subprojects.sourceSets.main.output)
	executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')

	reports {
		html.required = true
		xml.required = true
		csv.required = false
	}
}

task testReport(type: TestReport) {
	destinationDirectory = file("$buildDir/reports/allTests")
	testResults.from(subprojects.collect { it.tasks.withType(Test) })
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

sonarqube {
	properties {
		property "sonar.projectKey", "MarkusAmshove_natls"
		property "sonar.organization", "markusamshove"
		property "sonar.host.url", "https://sonarcloud.io"
		property 'sonar.coverage.jacoco.xmlReportPaths', "${rootProject.buildDir}/reports/jacoco/cover/cover.xml"
		property 'sonar.coverage.exclusions', 'tools/**/*,libs/natlint/src/main/java/org/amshove/natlint/cli/sinks/*.*'
	}
}

project.tasks.getByPath('natlint:test').dependsOn(project.tasks.getByPath('natparse:test'))
project.tasks['sonarqube'].dependsOn cover
